DEFINE INSTRUMENT LET(Ei=1.89, freq=100, string chopper="high resolution", string output_filename="mcstas.mcpl", string sample="plate")

DECLARE
%{
double v_foc;
double phase_time, E_min, E_max;
double PR, Res5, E_foc;
double pha_offset = 0;
double jitter = 0;
double slot_size;
int plate;
%}

INITIALIZE 
%{ 
E_foc = Ei;
v_foc = SE2V*sqrt(E_foc);
fprintf(stderr, "\nv_foc: %f\n\n",v_foc);
phase_time = 23.5 / v_foc + pha_offset;
E_min = 2.28e-3 * 23.5 / (phase_time+1.e-3); E_min *= E_min;
E_max = 2.28e-3 * 23.5 / (phase_time-1.e-3); E_max *= E_max;
PR = freq / 2.;
Res5 = freq;
plate = strcmp(sample, "plate")==0 ? 1 : 0;
for (int ii=0; chopper[ii]; ii++)
  chopper[ii] = tolower(chopper[ii]);
if(strstr(chopper, "res"))
  slot_size = 0.015;
else if(strstr(chopper, "flux"))
  slot_size = 0.031;
else
  slot_size = 0.020;
fprintf(stderr, "Using mode %s with slot size = %f mm\n", chopper, slot_size);
%}

TRACE

COMPONENT Origin = Arm()
  AT (0,0,0) ABSOLUTE

COMPONENT Source = ViewModISISver1(
    Face = "Let_Base.mcstas", E0 = E_min, E1 = E_max, dist = 1.68, xw = 0.04, yh = 0.09,
	modPosition=0,tally=8.4,modXsize=0.12,modZsize=0.12)
  AT (0, 0, 0) RELATIVE Origin 

COMPONENT shutter = Guide_channeled(
    w1 = 0.04, h1 = 0.09, w2 = 0.04, h2 = 0.09, l = 1.980, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 2, my = 3)
  AT (0, 0, 1.680) RELATIVE Origin

COMPONENT insert = Guide_channeled(
    w1 = 0.04, h1 = 0.09, w2 = 0.04, h2 = 0.09, l = 2.500, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 2, my = 3)
  AT (0, 0, 3.740) RELATIVE Origin

COMPONENT afterinsert = Guide_channeled(
    w1 = 0.04, h1 = 0.09, w2 = 0.04, h2 = 0.09, l = 1.514, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 2, my = 3)
  AT (0, 0, 6.300) RELATIVE Origin

COMPONENT Res1 = DiskChopper(
    radius = 0.279, nu = PR/2.0, nslit=6, xwidth=0.04, yheight=0.09, jitter=jitter, delay=7.83/v_foc+pha_offset)
  AT (0, 0, 7.83) RELATIVE Origin

COMPONENT Res1_counter = DiskChopper(
    radius = 0.279, nu = -PR/2.0, nslit=6, xwidth=0.04, yheight=0.09, jitter=jitter, delay=7.83/v_foc+pha_offset)
  AT (0, 0, 7.83+2e-6) RELATIVE Origin

COMPONENT betweenchop1and2 = Guide_channeled(
    w1 = 0.04, h1 = 0.09, w2 = 0.04, h2 = 0.09, l = 0.312, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 2, my = 3)
  AT (0, 0, 7.852) RELATIVE Origin

/*COMPONENT FO = Chopper(
    R = 0.6, f = 10*2*PI, n=1, w=1.2, j=jitter, pha=8.2/v_foc+pha_offset)
  AT (0, 0, 8.2) RELATIVE Origin*/

COMPONENT betweenchop2and3 = Guide_channeled(
    w1 = 0.04, h1 = 0.09, w2 = 0.04, h2 = 0.09, l = 3.499, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 2, my = 3)
  AT (0, 0, 8.236) RELATIVE Origin

COMPONENT PR = DiskChopper(
    radius = 0.29, nu = PR, nslit=2, xwidth=0.058, yheight=0.1, jitter=jitter, delay=11.75/v_foc+pha_offset)
  AT (0, 0, 11.75) RELATIVE Origin
/*  R = 0.29, f = PR*2*PI, n=2, w=0.058, j=jitter, pha=11.75/v_foc+pha_offset) */

COMPONENT betweenchop3and4 = Guide_channeled(
    w1 = 0.04, h1 = 0.09, w2 = 0.04, h2 = 0.09, l = 3.886, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 2, my = 3)
  AT (0, 0, 11.765) RELATIVE Origin

COMPONENT CR = DiskChopper(
    radius = 0.29, nu = Res5/2.0, nslit=6, xwidth=0.054, yheight=0.1, jitter=jitter, delay=15.66/v_foc+pha_offset)
  AT (0, 0, 15.66) RELATIVE Origin
/*  R = 0.29, f = Res5*PI, n=6, w=0.054, j=jitter, pha=15.66/v_foc+pha_offset) */

COMPONENT betweenchop4andatt = Guide_channeled(
    w1 = 0.04, h1 = 0.09, w2 = 0.04, h2 = 0.0639, l = 5.807, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 2, my = 3)
  AT (0, 0, 15.681) RELATIVE Origin

COMPONENT guide_move = Guide_channeled(
    w1 = 0.04, h1 = 0.0639, w2 = 0.0311, h2 = 0.05718, l = 0.8823, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 4, my = 4)
 AT (0, 0, 21.489) RELATIVE Origin

COMPONENT funnel = Guide_channeled(
    w1 = 0.031, h1 = 0.05711, w2 = 0.02, h2 = 0.04868, l = 1.117, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 4, my = 4)
 AT (0, 0, 22.373) RELATIVE Origin

COMPONENT C5 = DiskChopper(
    radius = 0.279, nu = Res5, nslit=2, xwidth=slot_size, yheight=0.0487, jitter=jitter, delay=23.5/v_foc+pha_offset)
  AT (0, 0, 23.5) RELATIVE Origin

COMPONENT C5_counter = DiskChopper(
    radius = 0.279, nu = -Res5, nslit=2, xwidth=slot_size, yheight=0.0487, jitter=jitter, delay=23.51/v_foc+pha_offset)
  AT (0, 0, 23.51) RELATIVE Origin

COMPONENT endguide = Guide_channeled(
    w1 = 0.02, h1 =0.0484 , w2 = 0.020, h2 = 0.04, l = 1.1, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 4, my = 4)
 AT (0, 0, 23.52) RELATIVE Origin

COMPONENT snot = Guide_channeled(
    w1 = 0.02, h1 =0.04 , w2 = 0.02, h2 = 0.04, l = 0.23, alphax = 4.38, alphay = 4.38 , //nslit=3,
 W=3e-3, mx = 2, my = 4)
 AT (0, 0, 24.622) RELATIVE Origin

COMPONENT detslit = Slit(xmin=-0.025, xmax=0.025, ymin=-0.025, ymax=0.025)
AT (0,0,25.0) RELATIVE Origin

COMPONENT mcplout = MCPL_output_horace(filename="mcstas.mcpl", plate=plate)
AT (0,0,25.0) RELATIVE Origin

END
