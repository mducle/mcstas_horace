/* %Parameters
* INPUT PARAMETERS:
* E_min  : lowest energy sampled      [meV]
* E_max  : highest energy sampled     [meV]
* E_foc : energy selected by chopper [meV]
* nu_hz : frequency of chopper rotation [Hz]
* type : chopper package selected [sloppy chopper = 1, B chopper = 2]
* m  : m value of guides
* 
*******************************************************************************/


DEFINE INSTRUMENT MERLIN(Ei=80.0, freq=200.0, string chopper="G", string output_filename="mcstas.mcpl", string sample="plate")

DECLARE
%{
double slit_curv,num_slits,width,len;
double phase_time, E_min, E_max;
int plate;
%}

// #include <math.h>

INITIALIZE 
%{
switch(chopper[0]) {
  case 'b':
  case 'B':
    slit_curv=1/0.82;
    num_slits=55;
    width=0.00114;
    len=0.099;
    fprintf(stderr,"MERLIN b chopper selected");
    break;
  case 's':
  case 'S':
    slit_curv=1/1.3;
    num_slits=28;
    width=0.00228;
    len=0.099;
    fprintf(stderr,"MERLIN Sloppy selected selected");
    break;
  case 'g':
  case 'G':
    slit_curv=0;
    num_slits=350;
    width=0.0002;
    len=0.01;
    fprintf(stderr,"Merlin Gd selected selected");
    break;
  default:
    fprintf(stderr,"Chopper Type not recognised\n");
    exit(1);
}
/* hardwired for FC position 10m from moderator */
phase_time = (2.28e-3*(10.0)/sqrt(Ei));
E_min = 2.28e-3 * 10.0 / (phase_time+5.e-4); E_min *= E_min;
E_max = 2.28e-3 * 10.0 / (phase_time-5.e-4); E_max *= E_max;
plate = strcmp(sample, "plate")==0 ? 1 : 0;
%}

TRACE

// define origin //
COMPONENT Origin = Arm()
  AT (0,0,0) ABSOLUTE

// The moderator //
COMPONENT isis_mod =
ViewModISISver1(Face="TS1verBase2016_LH8020_newVM-var_South04_Merlin.mcstas", E0 = E_min, E1 = E_max,
dist = 1.7, xw = 0.094, yh = 0.094, modPosition=0,tally=8.4,modXsize=0.12,modZsize=0.12)
 AT (0,0,0) RELATIVE Origin

// 1st piece of guide in shutter //
COMPONENT shutter_guide = Guide_channeled(
    w1 = 0.094, h1 = 0.094, w2 = 0.094, h2 = 0.094, l = 2.0, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 3, my = 3)
  AT (0, 0, 1.7) RELATIVE Origin

// 2nd piece of guide converging to chopper pit //
COMPONENT conv_guide = Guide_channeled(
    w1 = 0.094, h1 = 0.094, w2 = 0.067, h2 = 0.067, l = 4.760, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 3, my = 3)
  AT (0, 0, 3.704) RELATIVE Origin

// 3rd piece of guide leading to fermi chopper//
COMPONENT between_guide = Guide_channeled(
    w1 = 0.0629, h1 = 0.0629, w2 = 0.0594, h2 = 0.0594, l = 0.640, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 3, my = 3)
  AT (0, 0, 9.182) RELATIVE Origin

// the fermi chopper //
COMPONENT fermi = FermiChopper(delay=phase_time, radius=0.055, nu=-freq,
  yheight=0.07, w=width, nslit=num_slits, R0=0.0,
  Qc=0.02176, alpha=2.33, m=0, length=len, eff=0.95, curvature=-slit_curv, zero_time=0)
  AT (0,0,10) RELATIVE Origin//

// final guide section just bebore sample //
COMPONENT final_Guide = Guide_channeled(
    w1 = 0.0568, h1 = 0.0568, w2 = 0.0506, h2 = 0.0506, l = 1.10, alphax = 4.38, alphay = 4.38 ,
 W=3e-3, mx = 3, my = 3)
  AT (0, 0, 10.277) RELATIVE Origin

COMPONENT sampleslit2 = Slit(
    xmin = -0.025, xmax = 0.025, ymin = -0.025, ymax = 0.025)
  AT (0, 0, 11.38) RELATIVE Origin

COMPONENT mcplout = MCPL_output_horace(filename=output_filename, plate=plate)
  AT (0, 0, 11.79) RELATIVE Origin ROTATED (0, 0, 0) RELATIVE Origin

END
